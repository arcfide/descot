\title{Extended Definitions}

\chapter{Introduction}

This library defines a set of definition constructs.
This includes ways to define functions that ought to be integrated, 
as well as constant values.
[[define-auxilary-keywords]] is useful for explicitly exporting 
auxilary syntax.

\chapter{Definitions}

<<Definitions>>=
(define-syntax define-integrable
  (syntax-rules (lambda)
    [(_ name (lambda formals form1 form2 ...))
     (identifier? #'name)
     (begin
       (define-syntax name
         (lambda (x)
           (syntax-case x ()
             [_ (identifier? x) #'xname]
             [(_ arg (... ...))
              #'((fluid-let-syntax ([name (identifier-syntax xname)])
                   (lambda formals form1 form2 ...))
                  arg
                  (... ...))])))
       (define xname
         (fluid-let-syntax ([name (identifier-syntax xname)])
           (lambda formals form1 form2 ...))))]))

(define-syntax define-constant
  (syntax-rules ()
    [(_ name expr)
     (define-syntax name
       (let ([x expr])
         (lambda (y)
           (syntax-case y ()
             [id (identifier? #'id) #`'#,(datum->syntax #'id x)]))))]))

(define-syntax define-auxilary-keywords
  (syntax-rules ()
    [(_ aux ...)
     (begin
       (define-syntax aux
         (lambda (x)
           (errorf #f "misplaced aux keyword ~a"
             (syntax->datum x))))
       ...)]))
@

\chapter{R6RS Module}

<<extended-definitions.sls>>=
<<License>>

#!chezscheme
(library (arcfide extended-definitions)
  (export define-integrable define-constant define-auxilary-keywords)
  (import (scheme))

<<Definitions>>

)
@

\chapter{Chez Module}

<<extended-definitions.ss>>=
<<License>>

(module extended-definitions
  (define-integrable define-constant)
  (import scheme)

<<Definitions>>

)
@

\chapter{Free Form Definitions}

The following can be used 
to generate a simple file 
with only definitions and no module form.

<<extended-definitions.scm>>=
<<License>>

<<Definitions>>
@

\chapter{License}

This code is distributed under an ISC License.

<<License>>=
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Extended Definition Forms
;;; 
;;; Copyright (c) 2009 Aaron Hsu <arcfide@sacrideo.us>
;;; 
;;; Permission to use, copy, modify, and distribute this software for
;;; any purpose with or without fee is hereby granted, provided that the
;;; above copyright notice and this permission notice appear in all
;;; copies.
;;; 
;;; THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL
;;; WARRANTIES WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED
;;; WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE
;;; AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL
;;; DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA
;;; OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER
;;; TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
;;; PERFORMANCE OF THIS SOFTWARE.
@